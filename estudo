%Macro Envia_Email;
    proc sql noprint;
        select count(*) into :row_count from WORK.cria_dados_email;
    quit;

    %if &row_count > 0 %then %do;
        options emailauthprotocol=none
                emailid='audit.mefis@bb.com.br'
                emailsys=smtp
                emailhost='smtp.bb.com.br'
                emailport=25;
        filename newEmail email lrecl=64000;

        data _null_;
            SET work.cria_dados_email END=FINAL;

            /* Limpar os endereços de email */
            destino1 = compress(strip(destino), "'"" ");
            destino2 = compress(strip(destino_cc), "'"" ");
            destino3 = compress(strip(destino_cco), "'"" ");
            assunto = "Recomendação: " || strip(titulo) || ".";

            /* Depuração: imprimir valores no log */
            put 'DEBUG: destino1="' destino1 +(-1) '"';
            put 'DEBUG: destino2="' destino2 +(-1) '"';
            put 'DEBUG: destino3="' destino3 +(-1) '"';
            put 'DEBUG: assunto="' assunto +(-1) '"';

            /* Configurar o arquivo de email */
            file newEmail
                from  = "audit.mefis@bb.com.br"
                content_type = "text/html";

            /* Especificar destinatários e assunto */
            put '!EM_TO!' destino1;
            put '!EM_CC!' destino2;
            put '!EM_BCC!' destino3;
            put '!EM_SUBJECT!' assunto;

            /* Escrever o corpo do email */
            put mensagem_html;

            /* Enviar o email e iniciar um novo, se não for o último */
            put '!EM_SEND!';
            if not FINAL then
                put '!EM_NEWMSG!';
            else
                put '!EM_ABORT!';
        run;

        /* Limpar a referência ao arquivo */
        filename newEmail clear;
    %end;
%Mend;

%Envia_Email;
